# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: mosra <mosra@centrum.cz>
# PKGBUILD stolen from blender-svn
pkgname=blender-freestyle-svn
pkgver=24447
pkgrel=1
pkgdesc="SVN version of Blender (branch with FreeStyle edge rendering)"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libgl' 'mesa' 'libjpeg' 'libpng' 'openexr' 'gettext' 'python3' 'sdl' 'desktop-file-utils' 'libsamplerate' 'hicolor-icon-theme')
makedepends=('scons' 'subversion' 'yasm')
provides=('blender')
conflicts=('blender' 'blender2.4-svn' 'blender-svn')
license=('GPL')
install=blender.install
source=(blender.desktop blender.config blender-update-scripts)
md5sums=('e9c26e370839fd902eb76c98fe3b9589'
         '95bfa9ff47a745f40310d0312e6726b2'
         '05d8a9b8a293ba3bb752466540bf6b14')

_svntrunk="https://svn.blender.org/svnroot/bf-blender/branches/soc-2008-mxcurioni/"
_svnmod="blender"

build() {
  # get the sources
  cd "$srcdir"
  msg "Connecting to Blender SVN server......."
  if [ -d $_svnmod/.svn ]; then
  	cd $_svnmod && svn up -r $pkgver
  else
  	svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  msg "SVN checkout done or server timeout"

  msg "Starting make..."
  cd "$srcdir"/$_svnmod

  ##########
  # tweak scons config script
  # first translate CFLAGS and CXXFLAGS for use
  cp "$srcdir"/blender.config user-config.py
  _CFLAGS=`echo $CFLAGS | sed -e "s/\ -O.//" -e "s/^/'/" -e "s/\ /','/g" -e "s/$/'/"`
  _CXXFLAGS=`echo $CXXFLAGS | sed -e "s/\ -O.//" -e "s/^/'/" -e "s/\ /','/g" -e "s/$/'/"`
  sed "s|^REL_CFLAGS\ =\ \['.*'\]|REL_CFLAGS\ =\ \['`echo -n $CFLAGS | sed 's/.*\(-O[s,0-4]\).*/\1/g'`'\]|" -i user-config.py
  sed "s|^REL_CCFLAGS\ =\ \['.*'\]|REL_CCFLAGS\ =\ \['`echo -n $CXXFLAGS | sed 's/.*\(-O[s,0-4]\).*/\1/g'`'\]|" -i user-config.py
  sed "s|^CCFLAGS\ =\ \[|CCFLAGS\ =\ \[$_CFLAGS,|" -i user-config.py
  sed "s|^CXXFLAGS\ =\ \[|CXXFLAGS\ =\ \[$_CXXFLAGS,|" -i user-config.py
  unset _CFLAGS
  unset _CXXFLAGS
  ##########
  scons || return 1
  ##########
  # now we will compile some plugins
  cp -af "$srcdir"/install/linux2/plugins/* \
    "$srcdir"/$_svnmod/source/blender/blenpluginapi/
  cd "$srcdir"/$_svnmod/source/blender/blenpluginapi
  chmod 755 bmake
  make
  ###########
  # And...Install!!!
  cd "$srcdir"/install/linux2
  install -D -m755 blender "$pkgdir"/usr/bin/blender
  install -D -m755 "$srcdir"/blender-update-scripts "$pkgdir"/usr/bin/blender-update-scripts

  install -d -m755 "$pkgdir"/usr/share/$_svnmod
  cp -R .blender/locale "$pkgdir"/usr/share/
  cp -R .blender "$pkgdir"/usr/share/$_svnmod/
  install -d -m755 "$pkgdir"/usr/share/$_svnmod/plugins/{sequence,texture}
  cp "$srcdir"/$_svnmod/source/blender/blenpluginapi/sequence/*.so \
    "$pkgdir"/usr/share/$_svnmod/plugins/sequence/
  cp "$srcdir"/$_svnmod/source/blender/blenpluginapi/texture/*.so \
    "$pkgdir"/usr/share/$_svnmod/plugins/texture/

  install -D -m644 "$srcdir"/blender.desktop \
  	"$pkgdir"/usr/share/applications/blender.desktop

  # install icons
  cd icons
  for DIR in *; do
    if [ $DIR != "scalable" ]; then
      install -D -m644 $DIR/blender.png "$pkgdir"/usr/share/icons/hicolor/$DIR/blender.png
    else
      install -D -m644 $DIR/blender.svg "$pkgdir"/usr/share/icons/hicolor/$DIR/blender.svg
    fi
  done
}
