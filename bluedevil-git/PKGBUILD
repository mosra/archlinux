# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mosra <mosra@centrum.cz>

pkgname=bluedevil-git
pkgver=20110204
pkgrel=1
pkgdesc='The new bluetooth stack for KDE, itâ€™s composed of: KCM, KDED, KIO, Library and some other small applications'
arch=('i686' 'x86_64')
url="http://gitweb.kde.org/bluedevil.git"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil' 'obex-data-server' 'obexd-client')
makedepends=('cmake' 'automoc4' 'git')
provides=(bluedevil)
conflicts=(bluedevil)
source=()
md5sums=()

_gitroot="git://anongit.kde.org/bluedevil.git"
_gitname="bluedevil"

build(){
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Change remote url to anongit
        if [ -z $(git branch -v | grep anongit) ] ; then
            git remote set-url origin ${_gitroot}
        fi

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../${_gitname} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR=${pkgdir} install
}
