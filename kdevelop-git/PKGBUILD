# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-git
pkgver=20130924
pkgrel=1
pkgdesc="A C/C++ development environment for KDE - Git build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
groups=('kde')
depends=('kdevplatform-git' 'kdebase-workspace' 'kdesdk-okteta' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'perl' 'git')
optdepends=('kdesdk-okteta: hex editor'
            'kdebase-konsole: embedded terminal'
            'git: git support'
            'gdb: gdb support'
            'qt4-doc: qt4 documentation integration'
            'qt5-doc: qt5 documentation integration')
provides=('kdevelop')
conflicts=('kdevelop')
source=('qdbus.patch')
md5sums=('6567c88de52d984ce3049dc136c352c1')

_gitroot="git://anongit.kde.org/kdevelop"
_gitname="kdevelop"

pkgver() {
    date +%Y%m%d
}

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Revert old patches
        git checkout .

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."

    # Call `qdbus-qt4` instead of `qdbus`
    cd "$srcdir/$_gitname"
    git apply "$srcdir/qdbus.patch"

    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../$_gitname \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="$pkgdir" install
}
