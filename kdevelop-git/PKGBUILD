# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-git
pkgver=20130517
pkgrel=1
pkgdesc="A C/C++ development environment for KDE - Git build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
groups=('kde')
depends=('kdevplatform-git' 'kdebase-workspace' 'kdesdk-okteta' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'perl' 'git')
optdepends=('kdesdk-okteta: hex editor'
            'kdebase-konsole: embedded terminal'
            'git: git support'
            'gdb: gdb support'
            'qt-doc: qt documentation integration')
provides=('kdevelop')
conflicts=('kdevelop')

_gitroot="git://anongit.kde.org/kdevelop"
_gitname="kdevelop"

pkgver() {
    date +%Y%m%d
}

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."
    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../$_gitname \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="$pkgdir" install
}
