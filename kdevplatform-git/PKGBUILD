# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevplatform-git
pkgver=20140907
pkgrel=1
pkgdesc="A C/C++ development platform for KDE - Git build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
groups=('kde')
depends=('karchive' 'kconfig' 'kguiaddons' 'ki18n' 'kitemmodels' 'kitemviews' 'kjobwidgets' 'kcmutils' 'kio' 'knewstuff' 'knotifyconfig' 'kparts' 'kservice' 'sonnet' 'ktexteditor' 'threadweaver' 'kwindowsystem' 'kdeclarative' 'kdelibs4support' 'kdoctools' 'libkomparediff2-git' 'qt5-quick1' 'boost-libs')
makedepends=('cmake' 'boost' 'git')
optdepends=('kdesdk-kompare: difference checking')
provides=('kdevplatform')
conflicts=('kdevplatform')
install='kdevplatform.install'

_gitroot="git://anongit.kde.org/kdevplatform"
_gitname="kdevplatform"

pkgver() {
    date +%Y%m%d
}

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Revert old patches
        git checkout .

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."
    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../$_gitname \
        -DLibKompareDiff2_DIR=/opt/kf5/lib/cmake/LibKompareDiff2 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=lib \
        -DCMAKE_BUILD_TYPE=Release

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="$pkgdir" install
}
