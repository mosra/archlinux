# $Id: PKGBUILD 89935 2010-09-05 10:35:21Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Konstantinos Pachnis <kpachnis@mes-direct.com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=cmake-debug
pkgver=2.8.2
pkgrel=2
pkgdesc="A cross-platform open-source make system (debug version)"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.cmake.org"
depends=('expat' 'curl' 'gcc-libs' 'shared-mime-info')
makedepends=('qt')
optdepends=('qt: cmake-gui')
provides=('cmake')
conflicts=('cmake')
options=(!strip)
install=cmake.install
source=("http://www.cmake.org/files/v2.8/cmake-${pkgver}.tar.gz"
        "bootstrap.patch")
md5sums=('8c967d5264657a798f22ee23976ff0d9'
         'a7c14a96deddbb6f0cbc8052b4a983aa')

build() {
	cd $srcdir/cmake-$pkgver

	# Set CMAKE_BUILD_TYPE to Debug
	patch -p0 -i "${srcdir}/bootstrap.patch"

	./bootstrap --prefix=/usr \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--system-libs \
		--qt-gui \
		--parallel=2
	make
}

package() {
	cd ${srcdir}/cmake-${pkgver}
	make DESTDIR=${pkgdir} install

	vimpath="$pkgdir/usr/share/vim/vimfiles"
	install -Dm644 Docs/cmake-indent.vim $vimpath/indent/cmake-indent.vim
	install -Dm644 Docs/cmake-syntax.vim $vimpath/syntax/cmake-syntax.vim
	install -Dm644 Docs/cmake-mode.el $pkgdir/usr/share/emacs/site-lisp/cmake-mode.el
	install -Dm644 Copyright.txt $pkgdir/usr/share/licenses/cmake/LICENSE
}
