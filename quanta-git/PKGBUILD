# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mosra <mosra@centrum.cz>

pkgname=quanta-git
pkgver=20100618
pkgrel=1
pkgdesc="An IDE for Web Development based on the KDevelop Platform"
arch=('i686' 'x86_64')
url="http://quanta.kdewebdev.org/"
license=('GPL')
depends=('kdevplatform-git')
optdepends=('qjson' 'kdevelop-pg-qt-git')
makedepends=('cmake' 'automoc4' 'git' 'flex')
provides=('quanta')
replaces=('quanta' 'quanta-svn')
conflicts=('quanta' 'quanta-svn' 'kdevelop-extra-plugins-executebrowser-svn' 'kdevelop-extra-plugins-executescript-svn' 'kdevelop-extra-plugins-xdebug-svn')
source=('cmakelists.patch')
md5sums=('df6bedbacc751667d452e310df1c17dd')

_gitroot="git://gitorious.org/kdevelop/quanta.git"
_gitname="quanta"

build() {
    cd "${srcdir}"

    msg "Connecting to the GIT server...."

    if [[ -d "$srcdir/$_gitname" ]] ; then
        cd ${_gitname}

        # Revert patched files
        git checkout -- languages/xml/language/duchain/CMakeLists.txt \
            debuggers/crossfire/CMakeLists.txt

        git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot} ${_gitname}
    fi

    # Patch CMakeLists to fix linker errors
    cd "$srcdir/$_gitname"
    git apply ../cmakelists.patch

    msg "GIT checkout done"
    msg "Starting make..."

    mkdir -p "${srcdir}/build"
    cd "${srcdir}/build"

    cmake ../${_gitname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELEASE

    make || return 1
}

package() {
    cd "${srcdir}/build"
    make DESTDIR=${pkgdir} install || return 1
}
