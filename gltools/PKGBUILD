# Contributor: mosra <mosra@centrum.cz>
pkgname=gltools
pkgver=5
pkgrel=1
pkgdesc="GLTools library used in OpenGL SuperBible book"
arch=('i686' 'x86_64')
url="http://www.starstonesoftware.com/OpenGL/"
license=('BSD')
depends=('glew' 'mesa')
makedepends=('unzip' 'cmake')
source=("http://www.starstonesoftware.com/files/SB${pkgver}.zip"
        "CMakeLists.txt"
        "FindGLTools.cmake")
noextract=(SB${pkgver}.zip)
md5sums=('e7aafa353c62f8430d1168971400e0f2'
         '8dfcd10f4fa9860fd906e78cd60db0c2'
         'c01b34b966060c8743e8974c008f0f73')

build() {
    cd "$srcdir"

    # Extract sources and copy cmake files there
    unzip -qo SB${pkgver}.zip
    cp CMakeLists.txt FindGLTools.cmake SB${pkgver}/Src/GLTools

    mkdir -p build
    cd build

    cmake ../SB${pkgver}/Src/GLTools \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir/" install
}
