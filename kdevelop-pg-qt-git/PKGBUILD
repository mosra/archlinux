# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-pg-qt-git
pkgver=20110501
pkgrel=1
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used mainly by KDevelop language plugins"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org"
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('kdevelop-pg-qt')
source=('flex-without-bison.patch')
md5sums=('bf6c73d24fe212e6095585e5d49d8ede')

_gitroot="git://anongit.kde.org/kdevelop-pg-qt"
_gitname="kdevelop-pg-qt"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Remove the old 'empty file' hack, if it is still present
        if [ ! -s "$srcdir/$_gitname/examples/cool/cool_lexer.cpp" ] ; then
            rm "$srcdir/$_gitname/examples/cool/cool_lexer.cpp"
        fi

        # Revert the flex-without-bison.patch
        git checkout kdev-pg/CMakeLists.txt

        # Change remote url to anongit
        if [ -z $(git branch -v | grep anongit) ] ; then
            git remote set-url origin ${_gitroot}
        fi

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    # Another case when flex exists and bison doesn't. The file is just in
    # another directory than expected, just copy it where it belongs.
    cd "$srcdir/${_gitname}"
    git apply "${srcdir}/flex-without-bison.patch"

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../${_gitname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELWITHDEBINFO

    make || return 1
    make DESTDIR=${pkgdir} install || return 1
}
