# Contributor: prettyvanilla <prettyvanilla@lavabit.com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-valgrind-git
pkgver=20130517
pkgrel=1
pkgdesc="Valgrind integration plugin for KDevelop - Git build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
groups=('kde' 'kdevelop-plugins')
depends=('kdevplatform-git' 'valgrind')
optdepends=('kdevelop' 'kcachegrind' 'massif-visualizer')
makedepends=('cmake' 'automoc4' 'git')
provides=('kdevelop-valgrind')
conflicts=('kdevelop-valgrind')
source=('kdevvalgrind.desktop.patch')
md5sums=('9bdabd579f734812a0ab9927b219b90c')

_gitroot="git://anongit.kde.org/kdev-valgrind"
_gitname="valgrind"

pkgver() {
    date +%Y%m%d
}

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Revert kdevvalgrind.desktop.patch
        git checkout .

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."

    # fix KDevelop-Version in desktop-file for the plugin to work in current kdevelop-git
    cd "$srcdir/$_gitname"
    git apply "$srcdir/kdevvalgrind.desktop.patch"

    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../$_gitname \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="$pkgdir" install
}
