# Maintainer: prettyvanilla <prettyvanilla@lavabit.com>

_pkgname=kdev-valgrind
pkgname=kdevelop-valgrind-git
pkgver=770.7c5ed14
pkgrel=1
pkgdesc="A plugin offering full integration of the valgrind suite to KDevelop4"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/devtools/plugins/kdev-valgrind"
license=('GPL')
groups=('kdevelop-plugins')
depends=('kdevplatform' 'valgrind')
optdepends=('kdevelop' 'kcachegrind' 'massif-visualizer')
makedepends=('git' 'cmake' 'automoc4')
provides=('kdevelop-valgrind')
conflicts=('kdevelop-valgrind')
source=("git://anongit.kde.org/kdev-valgrind")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/$_pkgname"

  cmake -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
        -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  # fix KDevelop-Version in desktop-file for the plugin to work in current kdevelop-git
  sed -i 's/X-KDevelop-Version=14/X-KDevelop-Version=16/g' kdevvalgrind.desktop

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
