# Maintainer: Harley Laue <losinggeneration@gmail.com>

pkgname=(kdevelop-executebrowser-git)
pkgver=20120618
pkgrel=1
pkgdesc="A plugin for KDevelop that starts a script in a webbrowser"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
groups=('kde' 'kdevelop-plugins')
depends=('kdevplatform-git')
optdepends=('kdevelop-git')
makedepends=('cmake' 'automoc4' 'git')
provides=('kdevelop-executebrowser')
conflicts=('kdevelop-executebrowser')
source=()
md5sums=()

_gitroot="git://anongit.kde.org/kdev-executebrowser"
_gitname="executebrowser"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Change remote url to anongit
        if [ -z "$(git remote -v | grep anongit)" ] ; then
            git remote set-url origin ${_gitroot}
        fi

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    [ -d "$srcdir/build" ] && rm -rf "$srcdir/build"
    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../${_gitname} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_BUILD_TYPE=RELWITHDEBINFO \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
        -DCMAKE_INSTALL_PREFIX=/usr

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="${pkgdir}" install
}
