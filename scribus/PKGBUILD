# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: mosra <mosra@centrum.cz>

pkgname=scribus
pkgver=1.3.7
pkgrel=2
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
url="http://www.scribus.net"
license=('GPL')
install=scribus.install
depends=('libcups>=1.3.11' 'lcms>=1.18' 'qt' 'ghostscript>=8.70' \
         'libart-lgpl>=2.3.20' 'python>=2.6.2' 'libxml2>=2.7.3' 'cairo' \
         'desktop-file-utils' 'shared-mime-info')
makedepends=('cmake')
options=(!libtool !makeflags)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        CMakeLists.diff)
md5sums=('2c486389172c053ff4da38960e412690'
         '74c3d52681fc4f6972337d69f522cee1')

build() {
    # Fix HTML/ODT import plugins
    cd "${srcdir}/${pkgname}-${pkgver}/scribus"
    patch -p0 -i "${srcdir}/CMakeLists.diff"

	cd ${srcdir}
	mkdir -p build
	cd build
	cmake ../${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make || return 1
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install

	# fix icon path in .desktop file
	sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' \
	   ${srcdir}/${pkgname}-${pkgver}/scribus.desktop

	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/scribus.desktop \
	   ${pkgdir}/usr/share/applications/scribus.desktop
}
