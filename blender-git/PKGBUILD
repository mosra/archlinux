# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: XercesBlue (nullfied)
# Contributor: Mikael Eriksson (miffe)
# Contributor: mosra <mosra@centrum.cz>

pkgname=blender-git
pkgver=20140221
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite (Git version)"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libgl' 'python' 'desktop-file-utils' 'hicolor-icon-theme'
         'ffmpeg' 'fftw' 'openal' 'freetype2' 'libxi' 'openimageio' 'opencolorio'
         'openshadinglanguage' 'libtiff' 'libpng')
optdepends=('cuda: CUDA support in Cycles')
makedepends=('git' 'cmake' 'boost' 'mesa')
provides=('blender')
conflicts=('blender')
license=('GPL')
install=blender.install
source=(blender.desktop)
md5sums=('e9c26e370839fd902eb76c98fe3b9589')

_gitroot="http://git.blender.org/blender.git"
_gitname="blender"

# determine whether we can precompile CUDA kernels
_CUDA_PKG=`pacman -Qq cuda 2>/dev/null` || true
if [ "$_CUDA_PKG" != "" ]; then
    _EXTRAOPTS="-DWITH_CYCLES_CUDA_BINARIES=ON \
                -DCUDA_TOOLKIT_ROOT_DIR=/opt/cuda \
                -DCYCLES_CUDA_BINARIES_ARCH=sm_30"
fi

pkgver() {
  date +%Y%m%d
}

build() {
  cd "$srcdir"
  msg "Connecting to Git server..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "Updating Git submodules..."

  cd "$srcdir/$_gitname"
  git submodule update --init --recursive

  msg "Git checkout done."

  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  msg "Starting make..."

  cmake ../$_gitname \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_PLAYER=ON \
        -DWITH_FFTW3=ON \
        -DWITH_SYSTEM_GLEW=ON \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_PYTHON_INSTALL=OFF \
        -DPYTHON_VERSION=3.3m \
        -DWITH_MOD_OCEANSIM=ON \
        $_EXTRAOPTS

  # Make CUDA kernel, only one job, so it won't eat up too much RAM
  make -j1 cycles_kernel_cuda

  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/blender.desktop \
    "$pkgdir"/usr/share/applications/blender.desktop

  if [ -e "$pkgdir"/usr/share/blender/*/scripts/addons/cycles/lib/ ] ; then
    # make sure the cuda kernels are not stripped
    chmod 444 "$pkgdir"/usr/share/blender/*/scripts/addons/cycles/lib/*
  fi
}

# kate: indent-width 2;
