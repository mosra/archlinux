# Contributor: mosra <mosra@centrum.cz>

pkgname=('kdevcpptools-git' 'verktyg-git')
pkgver=20100614
pkgrel=1
pkgdesc="KDevelop plugin for estimation and porting of Qt3 to Qt4 source"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevplatform-git' 'kdevelop-git')
makedepends=('cmake' 'automoc4' 'perl' 'git')
source=()
md5sums=()

_kdevelopgitroot="git://gitorious.org/kdevelop/kdevelop.git"
_kdevelopgitname="kdevelop"
_kdevcpptoolsgitroot="git://gitorious.org/kdevcpptools/kdevcpptools.git"
_kdevcpptoolsgitname="kdevcpptools"
_gitroot="git://gitorious.org/kdevcpptools/verktyg.git"
_gitname="verktyg"

build() {
    cd "$srcdir"
    msg "Fetching KDevelop source..."

    if [ -d $_kdevelopgitname ] ; then
        cd $_kdevelopgitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_kdevelopgitroot $_kdevelopgitname
    fi

    cd "$srcdir"
    msg "Fetching KdevCppTools source..."

    if [ -d $_kdevcpptoolsgitname ] ; then
        cd $_kdevcpptoolsgitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_kdevcpptoolsgitroot $_kdevcpptoolsgitname
    fi

    cd "$srcdir"
    msg "Fetching Verktyg source..."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Building KDevCppTools..."

    # Link in languages dir from KDevelop
    ln -sf "$srcdir/$_kdevelopgitname/languages" \
        "$srcdir/$_kdevcpptoolsgitname/src/languages"

    mkdir -p "$srcdir/build-kdevcpptools"
    cd "$srcdir/build-kdevcpptools"

    cmake ../${_kdevcpptoolsgitname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELWITHDEBINFO

    make || return 1

    msg "Building Verktyg..."

    # Link in languages dir from KDevelop and libquery dir from KDevCppTools
    ln -sf "$srcdir/$_kdevelopgitname/languages" \
        "$srcdir/$_gitname/languages"
    ln -sf "$srcdir/$_kdevcpptoolsgitname/src/libquery" \
        "$srcdir/$_gitname/libquery"

    mkdir -p "$srcdir/build-verktyg"
    cd "$srcdir/build-verktyg"

    cmake ../${_gitname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELWITHDEBINFO

    make || return 1
}

package_kdevcpptools-git() {
    cd "$srcdir/build-kdevcpptools"
    make DESTDIR=${pkgdir} install || return 1
}

package_verktyg-git() {
    cd "$srcdir/build-verktyg"
    make DESTDIR=${pkgdir} install || return 1
}