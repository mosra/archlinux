# Maintainer: ruario
# Contributor: RobertMe
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
pkgname=opera-next # Set to opera if you want to replace your main/stable build
_bigrelease=12.00
_buildver=1467
_randomizer=next5
pkgver=${_bigrelease}_${_buildver}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Alpha version."
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
provides=('opera')
replaces=('opera-devel')
optdepends=('gstreamer0.10-base-plugins: HTML5 Video support' 'gstreamer0.10-good: HTML5 Video support')
install=opera-next.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386

[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/${_randomizer}_${_bigrelease}-${_buildver}/opera-next-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz
        'opera-next')

md5sums=('1230b7a98ee770eb821a8b7649a20fc2'
         'c7347e118164ad2ec9fb860dd0ee3bf4')
[ "$CARCH" = "x86_64" ] && md5sums=('11b9dc0ef1620f7df05635a3d0fd7166'
                                    'c7347e118164ad2ec9fb860dd0ee3bf4')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
#_opdistro="Arch Linux"

package() {

	# 'Install' Opera into $pkgdir
	opera-next-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr --name ${pkgname} --repackage "${pkgdir}/usr"
	install -D -m 644 "${pkgdir}/usr/share/${pkgname}/defaults/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

	# Disable VDPAU for flash
	[[ $(md5sum "${pkgdir}/usr/bin/opera-next" | cut -d' ' -f 1) = "8ca4e95eb82df72bed1c2a944f062fec" ]]
	cp "${srcdir}/opera-next" "${pkgdir}/usr/bin/opera-next"

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p "${pkgdir}/usr/share/${pkgname}/custom/defaults"
		echo "[ISP]" > "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		echo "Id=${_opdistro}" >> "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		chmod 644 "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
	fi
}
