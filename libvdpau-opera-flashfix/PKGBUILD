# $Id: PKGBUILD 198259 2013-10-30 13:46:56Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgname=libvdpau
pkgname=libvdpau-opera-flashfix
pkgver=0.7
pkgrel=2
pkgdesc="Nvidia VDPAU library which fixes blue tint on Flash videos also on Opera browser"
arch=(i686 x86_64)
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
depends=('gcc-libs' 'libxext')
conflicts=('nvidia-utils<190.42-2' 'libvdpau')
provides=('libvdpau')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${_pkgname}-${pkgver}.tar.gz
        opera-flash.patch)
makedepends=('dri2proto')
sha256sums=('24dc08467ce8c59d6cfbf0d34d2dd1f86b4ff62e90777e0a8f513c5c9de9bce0'
            'b41f4b52e7acceedbdb1399b215a139e4c4a7fe7e395d9102143c1748febe3ac')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -p1 < ${srcdir}/opera-flash.patch

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
