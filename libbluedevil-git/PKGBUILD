# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mosra <mosra@centrum.cz>

pkgname=libbluedevil-git
pkgver=20110204
pkgrel=1
pkgdesc='A Qt wrapper for bluez used in the new KDE bluetooth stack'
arch=('i686' 'x86_64')
url="http://gitweb.kde.org/libbluedevil.git"
license=('GPL')
depends=('qt' 'bluez')
makedepends=('cmake' 'git')
provides=("libbluedevil")
conflicts=("libbluedevil")
source=()
md5sums=()

_gitroot="git://anongit.kde.org/libbluedevil.git"
_gitname="libbluedevil"

build(){
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Changre remote url to anongit
        if [ -z $(git branch -v | grep anongit) ] ; then
            git remote set-url origin ${_gitroot}
        fi

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../${_gitname} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR=${pkgdir} install
}
