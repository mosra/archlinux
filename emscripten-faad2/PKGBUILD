# Maintainer: Eric BÃ©langer <eric@archlinux.org>

_pkgname=faad2
pkgname=emscripten-faad2
pkgver=2.8.8
pkgrel=2
pkgdesc="ISO AAC audio decoder (Emscripten WASM build)"
arch=('x86_64')
url="http://faac.sourceforge.net/"
license=('GPL')
depends=('emscripten')
options=('!makeflags' 'staticlibs')
source=(https://downloads.sourceforge.net/sourceforge/faac/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('985c3fadb9789d2815e50f4ff714511c79c2710ac27a4aaaf5c0c2662141426d')

build() {
  cd ${_pkgname}-${pkgver}
  export CFLAGS='-s WASM=1 -DNDEBUG -O3'
  export CXXFLAGS='-s WASM=1 -DNDEBUG -O3'
  export LDFLAGS='-O3 -flto'
  emconfigure ./configure --prefix=/usr/lib/emscripten/system --enable-shared=no
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  emmake make DESTDIR="${pkgdir}" install

  rm -r ${pkgdir}/usr/lib/emscripten/system/bin
  rm ${pkgdir}/usr/lib/emscripten/system/lib/*.la
  rm -r ${pkgdir}/usr/lib/emscripten/system/share
}
